# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles
export ZSH=$HOME/.oh-my-zsh
# your project folder that we can `c [tab]` to
export PROJECTS=~/Work

plugins=(xcode kubectl)

source $ZSH/oh-my-zsh.sh

# all of our zsh files
typeset -U config_files

config_files=($DOTFILES/*/*.zsh)

# load the export files (they export env vars) that aren't private
for file in ${(M)config_files:#*/exports.zsh}
do
  source $file
done

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path, completion, and export files
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/exports.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit -i
else
  compinit -C -i
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# load antibody plugins
source ~/.zsh_plugins.sh
